options{
    IGNORE_CASE=true;
    JJTREE_OUTPUT_DIRECTORY="wci/frontend";
    NODE_EXTENDS="wci.intermediate.icodeimpl.ICodeNodeImpl";
    MULTI=true;
    VISITOR=true;
   DEBUG_PARSER=false;
   DEBUG_TOKEN_MANAGER=false;
}

PARSER_BEGIN(TypeScriptParser)
package wci.frontend;
import java.util.*;
import java.util.ArrayList;
import java.io.*;

import wci.intermediate.*;
import wci.intermediate.symtabimpl.*;
import wci.backend.*;
import wci.util.*;

import wci.intermediate.icodeimpl.ICodeNodeImpl;
import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.symtabimpl.DefinitionImpl.*;
import static wci.intermediate.symtabimpl.RoutineCodeImpl.*;
import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.icodeimpl.ICodeKeyImpl.*;

public class TypeScriptParser
{
    private static SymTabStack symTabStack;
    private static SymTabEntry programId;
	public static boolean generate_code_flag = true;
    public static void main(String[] args)
        throws Exception
    {
    	// Create and initialize the symbol table stack.
        symTabStack = SymTabFactory.createSymTabStack();
        Predefined.initialize(symTabStack);
        
        String sourceFilePath = args[0];
        
        // Parse a Typescript program.
        Reader    reader = new FileReader(sourceFilePath);
        TypeScriptParser parser = new TypeScriptParser(reader);
        SimpleNode rootNode = parser.program();
		rootNode.dump(">");
        // Print the cross-reference table.
		CrossReferencer crossReferencer = new CrossReferencer();
		crossReferencer.print(symTabStack);

        // Visit the parse tree nodes to decorate them with type information.
        TypeSetterVisitor typeVisitor = new TypeSetterVisitor();
        rootNode.jjtAccept(typeVisitor, null);

        // Create and initialize the ICode wrapper for the parse tree.
        ICode iCode = ICodeFactory.createICode();
        iCode.setRoot(rootNode);
        programId.setAttribute(ROUTINE_ICODE, iCode);
        
        // Print the parse tree.
        ParseTreePrinter treePrinter = new ParseTreePrinter(System.out);
        treePrinter.print(symTabStack);
		if(generate_code_flag)
		{
	        Backend backend = BackendFactory.createBackend("compile");
	        backend.process(iCode, symTabStack, objectFileName(sourceFilePath));
		}
		else
		{			System.out.println("COULD NOT GENERATE CODE DUE TO SYNTAX ERRORS");
		}
    }

    private static String fileNameWithoutExtension(String fileName) {
        int dotIndex = fileName.lastIndexOf('.');
        return fileName.substring(0, dotIndex);
    }

    private static String objectFileName(String fileName) {
        return fileNameWithoutExtension(fileName) + ".j";
    }
}
PARSER_END(TypeScriptParser)

SKIP: {
  " " | "\r" | "\t" | "\n"
  | <"//" (~["\n"])* ("\n"|"\r")?>
}

TOKEN : { //useful regex
	<#DIGIT  : ["0"-"9"]>
    |<#ONE_TO_NINE: ["1"-"9"] >
	//A-Z only if ignore case option is false
	| <#LETTER : ["a"-"z","A"-"Z"]>
	| <#SPACE: [" "] >
}

TOKEN : {//Reserved words
	<VAR : "var">
	| <IF : "if">
	| <ELSE_IF :"else if">
 	| <ELSE : "else">
 	| <FUNCTION : "function">
 	| <BOOLEAN : "boolean">
 	| <NUMBER : "number">
 	| <STRING : "char"> // made string parse as char so we can use pascal string
 	| <ENUM : "enum">
 	| <INTERFACE : "interface">
 	| <RETURN : "return">
 	| <VOID : "void">
 	| <WHILE : "while">
 	// Println is considered a reserved word for now. Might just want to make it part of the runtime library later.
 	| <PRINTLN : "println">
 	| <TRUE: "true" >
 	| <FALSE: "false" >
}

TOKEN : {//Symbols
	<NOT : "!"> 
	| <AMPRSAND : "&">
	| <MUL : "*">
	| <MINUS : "-">
	| <PLUS : "+">
	| <EQ : "=">
	| <BAR : "|">
	| <DIV : "/">
	| <COLON : ":">
	| <SEMICOLON : ";">
	| <QM : "?">
	| <COMMA : ",">
	| <DOT : ".">
	| <SINGLE_QUOTE : "'">
	| <QUOTE : "\"">
	| <LEFT_PARAN : "(">
	| <RIGHT_PARAN : ")">
	| <LEFT_BRAKET : "[">
	| <RIGHT_BRAKET : "]">
	| <LEFT_BRACE : "{"> 
	| <RIGHT_BRACE : "}">
	| <UNDER_SCORE : "_">
	| <LT : "<">
	| <GT : ">">
	| <LE : "<=">
	| <GE : ">=">
	| <DOUBLE_EQ : "==">
	| <OR : "||">
	| <AND : "&&">
	| <NOT_EQ : "!=">
}

TOKEN :
{
  <MATH_OP : <PLUS> | <MINUS> | <MUL> | <DIV> > 
}

TOKEN : {//Character String and Strings
	<STRING_LITERAL : <QUOTE> (~["\""])* <QUOTE> | <SINGLE_QUOTE> (~[]){1} <SINGLE_QUOTE> >
}

TOKEN : {//Identifier
	<IDENTIFIER : <LETTER> (<LETTER> | <DIGIT> | "_")*>
}

/* TypeScript treats real and integers as Numbers*/
TOKEN : {//Numbers
	<NUM: <INTEGER> | <REAL>> 
	| <INTEGER: (<SIGN>)? (<DIGIT>)+ | (<ONE_TO_NINE> (<DIGIT>)+) >
	| <REAL: (<SIGN>)? (<DIGIT>)+ "." | (<SIGN>)? "." (<DIGIT>)+ | (<SIGN>)? (<DIGIT>)+ "." (<DIGIT>)+ >

	//for simple version you cant have -5 parse as number 
 	| <#SIGN: "" >
}

TOKEN: { /*	Error token */
    <ERROR : ~["\r", "\n"]>
}

SimpleNode program() : {}
{
	// Cross listing requires a program ID don't wana take it out
	{	  
	programId = symTabStack.enterLocal("TypeScript");
	programId.setTypeSpec(Predefined.charType);
	programId.setDefinition(DefinitionImpl.PROGRAM);
	programId.setAttribute(ROUTINE_SYMTAB, symTabStack.push());
	programId.setAttribute(FUNCTIONS_CODE, new ArrayList<SimpleNode>());
	symTabStack.setProgramId(programId);
	}
        Statement_list()
        {
            return jjtThis;
        }
}


/*if statements*/
void boolean_op() : {jjtThis.setAttribute(IS_WHILE, false);}
{
	<LT> #LESS_THAN |
	 <GT> #GREATER_THAN |
	  <LE> #LESS_THAN_OR_EQUALS |
	   <GE> #GREATER_THAN_OR_EQUALS |
	    <DOUBLE_EQ> #EQUALITY |
	     <NOT_EQ> #NOT_EQUALS | 
	     <OR> #OR |
	      <AND> #AND
}

void condition() : {}
{
	<LEFT_PARAN>
	(Expression() (boolean_op() Expression())? )
	<RIGHT_PARAN>
}


//if statement grammar 
void if_body(): {}
{
	Compound_stmt()
}

void if_stmt() :{}
{
	if_part() (else_part())?
}

void if_part(): {}
{
	<IF> condition() if_body()
}

void else_part(): {}
{
	<ELSE> if_body()
}


void while_node() : {}
{
	<WHILE> condition() Compound_stmt()
}


void Statement() #void : {}
{
   	try
	{
	  LOOKAHEAD(3)
     Assignment() | if_stmt() | while_node() | println() | FunctionDeclaration()
   		| Function_invocation()
    } catch(ParseException ex)
   {
		HashSet syncSet = new HashSet();
        syncSet.add(SEMICOLON);
        syncSet.add(LEFT_BRACE);
        syncSet.add(RIGHT_BRACE);
        syncSet.add(EOF);
        handleError(ex, syncSet, true);
   }
}

void Compound_stmt() : {}
{
  	<LEFT_BRACE> Statement_list() <RIGHT_BRACE>
}

void Statement_list() #void: {}
{
  		(
  	  		Statement()
  	  	)+
}

void println() : {}
{
    <PRINTLN> <LEFT_PARAN> Expression() <RIGHT_PARAN> <SEMICOLON>
}

//------- Function rules begin -------

void PureFunction() : {}
{
    <LEFT_BRACE> Statement_list() (<RETURN> (Expression())? <SEMICOLON>)? <RIGHT_BRACE>
}

void FunctionDeclaration():{SymTabEntry funcID; SimpleNode funcNode;}
{
    <FUNCTION> <IDENTIFIER>
    {
      // enter the function name into a symbol table stack
      funcID = symTabStack.enterLocal(token.image);
      funcID.setDefinition(DefinitionImpl.FUNCTION);
      funcID.appendLineNumber(token.beginLine);
	  jjtThis.setAttribute(ID, funcID);
	  //add all function decl to a list do 
      ArrayList<SimpleNode> funcArr = (ArrayList<SimpleNode>) programId.getAttribute(FUNCTIONS_CODE);
      funcArr.add(jjtThis);
    }
	<LEFT_PARAN>  
  {
  }
   (
	      <IDENTIFIER> {
	          
	      }  
	      <COLON>
	      typeSignature() {
	        	
	      } 
		  (
		      <COMMA>
		      <IDENTIFIER> {
		          
		      }  
		      <COLON>
		      typeSignature() {
		        	
		      }
	     )*
   )?
     {
  	}
    <RIGHT_PARAN>
    <COLON>
   	 typeSignature() {
      // use funcID to set the typespec
     }
     
     PureFunction() {
	      // use funcID to set the ROUTINE_CODE
		  // use typespec  or set ROUTINE_RETURN_TYPE to void  or use typspec for both
     }
}

void Function_invocation() :{SymTabEntry funcID; SimpleNode funcNode;}
{
  <IDENTIFIER>
    {
      funcID = symTabStack.lookup(token.image);
      funcID.setDefinition(DefinitionImpl.FUNCTION);
      funcID.appendLineNumber(token.beginLine);
	  jjtThis.setAttribute(ID, funcID);  
    }
	<LEFT_PARAN>  
   (
     Expression() {
	          
	      }  
		(
		      <COMMA>
		      Expression() {
		          
		      }  
	     )*
   )?
   <RIGHT_PARAN>
   <SEMICOLON>
}
//------- Function rules end -------

void declaration() :{}
{
  VarIdentifierType() (<EQ> Expression())? <SEMICOLON>
}

void Assignment() :{}
{
  VarIdentifierType() (<EQ> Expression())? <SEMICOLON>|
		<IDENTIFIER> 
		{
		  SymTabEntry typeId = symTabStack.lookup(token.image);
          typeId.appendLineNumber(token.beginLine);
          SymTabEntry variableId = symTabStack.lookup(token.image);
		  jjtThis.setAttribute(ID, variableId);
		  jjtThis.setTypeSpec(typeId.getTypeSpec());
		} #identifier  <EQ> Expression() <SEMICOLON> 
}

void VarIdentifierType() #void : {}
{
    <VAR> IdentifierType()
}

void IdentifierType() #void :{SymTabEntry someID; ICodeNodeImpl old;}
{
    <IDENTIFIER> 
    {
      someID = symTabStack.enterLocal(token.image);
      processVariableDecl(token, someID);
	  jjtThis.setAttribute(ID, someID);  
      
    } #identifier <COLON>
    typeSignature()
    {
      SymTabEntry typeId = symTabStack.lookup(token.image);
      typeId.appendLineNumber(token.beginLine);
      TypeSpec type = typeId.getTypeSpec();
      someID.setTypeSpec(type);
    }
}

void typeSignature() #void :{}
{
	   <NUMBER>
	  |<BOOLEAN>
	  |<STRING>
	  |<VOID>
}

void Expression() #void : {}
{
    term() (
        "+" term()
        {
           jjtThis.setTypeSpec(Predefined.numberType);
          
        } #add(2)
      | "-" term()
      {
           jjtThis.setTypeSpec(Predefined.numberType);
        
      } #subtract(2)
    )*
}

void simple_expression() #void :{}
{
	factor() (("+"|"-"|"/"|"*") simple_expression())?
}

void term() #void : {}
{
    factor() (
        "*" factor()
        {
           jjtThis.setTypeSpec(Predefined.numberType);
          
        } #multiply(2)
      | "/" factor()
      {
           jjtThis.setTypeSpec(Predefined.numberType);
        
      } #divide(2)
    )*
}

void factor() #void : {}
{
  boolean_node()|
    identifier()
  | number()
  | string()
}

void boolean_node() : {}
{
  (<TRUE> | <FALSE>)
  {
	SymTabEntry variableId = symTabStack.lookup(token.image);
	variableId.appendLineNumber(token.beginLine);
	TypeSpec type = variableId.getTypeSpec();
	jjtThis.setTypeSpec(type);
	jjtThis.setAttribute(ID, variableId);	 
  } 
}

void identifier() : {} 
{
  <IDENTIFIER>
  {
	SymTabEntry variableId = symTabStack.lookup(token.image);
	variableId.appendLineNumber(token.beginLine);
	TypeSpec type = variableId.getTypeSpec();
	jjtThis.setTypeSpec(type);
	// ID is not really used but might need it later
	jjtThis.setAttribute(ID, variableId); 
  } 
}

// try parse integer if not try float if not error 
void number() : {}
{
  <NUM>
  {
  	  jjtThis.setTypeSpec(Predefined.numberType);
      jjtThis.setAttribute(VALUE, Double.parseDouble(token.image));
  }
}

void string() : {} 
{
  <STRING_LITERAL>
  {
  	  jjtThis.setTypeSpec(Predefined.charType);
	  jjtThis.setAttribute(VALUE, token.image);
  }
}

void error() : {}
{
	<ERROR>
}

  
JAVACODE
//return the token name in string format
void processVariableDecl(Token token, SymTabEntry variableId) #void
{
    variableId.setDefinition(DefinitionImpl.VARIABLE);
    variableId.appendLineNumber(token.beginLine);
}

JAVACODE
String handleError(ParseException ex, HashSet syncSet, boolean doPop) 
{
  	generate_code_flag = false;
    Token token = ex.currentToken;
    TypeScriptParseException tse = new TypeScriptParseException(ex);
    System.out.println(tse.getMessage());
    while (!syncSet.contains(token.kind))
    {
        token = getNextToken();
    }
    System.out.println(getToken(1).image);
    while (!syncSet.contains(token.kind))
    {
        token = getNextToken();
    }

    return token.image;
}